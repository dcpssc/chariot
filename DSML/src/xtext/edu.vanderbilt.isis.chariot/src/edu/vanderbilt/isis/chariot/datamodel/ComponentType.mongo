import org.bson.types.ObjectId
import edu.vanderbilt.isis.chariot.datamodel.SupportedOS
import edu.vanderbilt.isis.chariot.datamodel.SupportedMiddleware
import java.util.List
import com.mongodb.DB
import java.util.ArrayList
import com.mongodb.DBObject
import com.mongodb.DBCollection
import edu.vanderbilt.isis.chariot.datamodel.NodeCategory.DM_Memory
import edu.vanderbilt.isis.chariot.datamodel.NodeCategory.DM_Storage
import edu.vanderbilt.isis.chariot.datamodel.TimeUnit
import edu.vanderbilt.isis.chariot.generator.ConfigSpaceGenerator

package edu.vanderbilt.isis.chariot.datamodel.ComponentType {
	/*
	 * An entity to store time and unit.
	 */
	DM_Time {
		double time
		String unit
			
		/*
		 * Method to set unit of time.
		 * 
		 * @param unit	Unit of time.
		 */
		void setUnit (TimeUnit unit) {
			this.unit = unit.toString()
		}
	}
	
	/*
	 * An entity to store component type information.
	 */
	DM_ComponentType {
		ObjectId _id
		String name
		String providedFunctionality
		DM_Memory requiredMemory
		DM_Storage requiredStorage

		String requiredOS
		String requiredMiddleware
		List<String> requiredArtifacts
		List<String> requiredDevices
		
		// NOTE: Start and stop scripts are only relevant for
		// external components and not CHARIOT components.
		String startScript
		String stopScript

		DM_Time period
		DM_Time deadline
		
		/*
		 * Initialization method.
		 */
		void init () {
			this.name = new String()
			this.providedFunctionality = new String()
			this.requiredMemory = new DM_Memory => [
				setMemory (0)
				setUnit ("")
			]
			this.requiredStorage = new DM_Storage => [
				setStorage (0)
				setUnit ("")
			]			
			this.requiredOS = new String()
			this.requiredMiddleware = new String()
			this.requiredArtifacts = new ArrayList<String>()
			this.requiredDevices = new ArrayList<String>()
			this.startScript = new String()
			this.stopScript = new String()
			this.period = new DM_Time () => [
				setTime (0.0)
				setUnit ("")
			]
			this.deadline = new DM_Time () => [
				setTime (0.0)
				setUnit ("")
			]
		}
		
		/*
		 * Method to set memory requirement.
		 * 
		 * @param initializer	The DM_Memory entity that should be used to
		 * 						set required memory.
		 */
		void setRequiredMemory ((DM_Memory)=>void initializer) {
			this.requiredMemory = new DM_Memory => initializer
		}
		
		/*
		 * Method to set storage requirement.
		 * 
		 * @param initializer	The DM_Storage entity that should be used to 
		 * 						set required storage.
		 */
		void setRequiredStorage ((DM_Storage)=>void initializer) {
			this.requiredStorage = new DM_Storage => initializer
		}
		
		/*
		 * Method to set required OS.
		 * 
		 * @param os	The required OS.
		 */
		void setRequiredOS (SupportedOS os) {
			this.setRequiredOS (os.toString())
		}
		
		/*
		 * Method to set required middleware.
		 * 
		 * @param middleware	The required middleware.
		 */
		void setRequiredMiddleware (SupportedMiddleware  middleware) {
			this.setRequiredMiddleware (middleware.toString())
		}
		
		/*
		 * Method to add a required artifact.
		 * 
		 * @param artifact	Artifact to be added to the artifact requirement list.
		 */
		void addRequiredArtifact (String artifact) {
			if (!this.requiredArtifacts.contains(artifact))
				this.requiredArtifacts.add (artifact)
			else
				ConfigSpaceGenerator.LOGGER.info (artifact + 
					" artifact already exists in component " + this.name)
		}
		
		/*
		 * Method to add a required device.
		 * 
		 * @param device	Device to be added to the device requirement list.
		 */
		void addRequiredDevice (String device) {				
			if (!this.requiredDevices.contains(device))
				this.requiredDevices.add (device)
			else
				ConfigSpaceGenerator.LOGGER.info (device + 
					" device already exists in component " + this.name)
		}
		
		/*
		 * Method to set period.
		 * 
		 * @param initializer	DM_Time entity that should be used to set period.
		 */
		void setPeriod ((DM_Time)=>void initializer) {
			this.period = new DM_Time => initializer
		}
		
		/*
		 * Method to set deadline.
		 * 
		 * @param initializer	DM_Time entity that should be used to set deadline.
		 */
		void setDeadline ((DM_Time)=>void initializer) {
			this.deadline = new DM_Time => initializer
		}
		
		/*
		 * Method to insert a component type entity into a database.
		 * 
		 * @param database	Database where the component type entity should 
		 * 					be inserted.
		 */
		void insert (DB database) {			
			val dbCollection = database.getCollection('ComponentTypes')

			// Check if already exists in database. Store only if no existing
			// component type has the same name.
			val result = dbCollection.findOne((new DM_ComponentType => [
				name = this.name
			]).getDbObject())
				
			if (result == null) {
				dbCollection.save(this.getDbObject())
				ConfigSpaceGenerator.LOGGER.info (this.name + 
					" component type added to database")
			}
			else {
				ConfigSpaceGenerator.LOGGER.info (this.name + 
					" component type already exists. Trying to update")
					
				this.update(result, dbCollection)
			}
		}
		
		/*
		 * Method to update an existing component type entity.
		 * 
		 * @param objectToUpdate	Component type entity to update.
		 * @param targetCollection	Collection where the component type entity
		 * 							is located in the database.
		 */
		 void update (DBObject objectToUpdate, DBCollection targetCollection) {		 	
		 	// NOTE: Naive update where we replace (remove old and store new)
		 	// exiting entity.
		 	targetCollection.remove(objectToUpdate)
		 	targetCollection.save(this.getDbObject())
		 	
		 	ConfigSpaceGenerator.LOGGER.info (this.name + 
				" component type has been updated.")
		 }
	}
}